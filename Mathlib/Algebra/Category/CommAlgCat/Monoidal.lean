/-
Copyright (c) 2025 Ya√´l Dillies, Christian Merten, Micha≈Ç Mruga≈Ça, Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies, Christian Merten, Micha≈Ç Mruga≈Ça, Andrew Yang
-/
import Mathlib.Algebra.Category.CommAlgCat.Basic
import Mathlib.CategoryTheory.Monoidal.Cartesian.Basic

/-!
# The cocartesian monoidal category structure on commutative `R`-algebras

This file provides the cocartesian-monoidal category structure on `CommAlgCat R` constructed
explicitly using the tensor product.
-/

open CategoryTheory CartesianMonoidalCategory Limits TensorProduct

noncomputable section

namespace CommAlgCat
universe u v
variable {R : Type u} [CommRing R] {A B : CommAlgCat.{u} R}

variable (A B) in
/-- The explicit cocone with tensor products as the fibered product in `CommAlgCat`. -/
def binaryCofan : BinaryCofan A B :=
  .mk (ofHom Algebra.TensorProduct.includeLeft)
    (ofHom (Algebra.TensorProduct.includeRight (A := A)))

variable (A B) in
@[simp]
lemma binaryCofan_inl : (binaryCofan A B).inl = ofHom Algebra.TensorProduct.includeLeft := rfl

variable (A B) in
@[simp]
lemma binaryCofan_inr : (binaryCofan A B).inr = ofHom Algebra.TensorProduct.includeRight := rfl

variable (A B) in
@[simp] lemma binaryCofan_pt : (binaryCofan A B).pt = .of R (A ‚äó[R] B) := rfl

variable (A B) in
/-- Verify that the pushout cocone is indeed the colimit. -/
def binaryCofanIsColimit : IsColimit (binaryCofan A B) :=
  BinaryCofan.IsColimit.mk _
    (fun f g ‚Ü¶ ofHom (Algebra.TensorProduct.lift f.hom g.hom fun _ _ ‚Ü¶ .all _ _))
    (fun f g ‚Ü¶ by ext1; exact Algebra.TensorProduct.lift_comp_includeLeft _ _ fun _ _ ‚Ü¶ .all _ _)
    (fun f g ‚Ü¶ by ext1; exact Algebra.TensorProduct.lift_comp_includeRight _ _ fun _ _ ‚Ü¶ .all _ _)
    (fun f g m hm‚ÇÅ hm‚ÇÇ ‚Ü¶ by
      ext1
      refine Algebra.TensorProduct.liftEquiv.symm_apply_eq (y := ‚ü®‚ü®_, _‚ü©, fun _ _ ‚Ü¶ .all _ _‚ü©).mp ?_
      exact Subtype.ext (Prod.ext congr(($hm‚ÇÅ).hom) congr(($hm‚ÇÇ).hom)))

/-- The initial object of `CommAlgCat R` is `R` as an algebra over itself -/
def isInitialSelf : IsInitial (of R R) := .ofUniqueHom (fun A ‚Ü¶ ofHom (Algebra.ofId R A))
  fun _ _ ‚Ü¶ hom_ext (Algebra.ext_id _ _ _)

open Opposite Algebra.TensorProduct CommAlgCat

attribute [local ext] Quiver.Hom.unop_inj

instance : MonoidalCategory (CommAlgCat.{u} R)·µí·µñ where
  tensorObj S T := op <| of R (S.unop ‚äó[R] T.unop)
  whiskerLeft S {T‚ÇÅ T‚ÇÇ} f := .op <| ofHom (Algebra.TensorProduct.map (.id _ _) f.unop.hom)
  whiskerRight {S‚ÇÅ S‚ÇÇ} f T := .op <| ofHom (Algebra.TensorProduct.map f.unop.hom (.id _ _))
  tensorHom {S‚ÇÅ S‚ÇÇ T‚ÇÅ T‚ÇÇ} f g := .op <| ofHom (map f.unop.hom g.unop.hom)
  tensorUnit := .op (.of R R)
  associator {S T U} := .op <| isoMk (Algebra.TensorProduct.assoc R R _ _ _).symm
  leftUnitor S := .op <| isoMk (Algebra.TensorProduct.lid R _).symm
  rightUnitor _ := .op <| isoMk (Algebra.TensorProduct.rid R R _).symm
  tensorHom_def := by intros; ext <;> rfl
  tensor_id := by intros; ext <;> rfl
  tensor_comp := by intros; ext <;> rfl
  whiskerLeft_id := by intros; ext <;> rfl
  id_whiskerRight := by intros; ext <;> rfl
  associator_naturality := by intros; ext <;> rfl
  leftUnitor_naturality := by intros; rfl
  rightUnitor_naturality := by intros; rfl
  pentagon := by intros; ext <;> rfl
  triangle := by intros; ext <;> rfl

instance : CartesianMonoidalCategory (CommAlgCat.{u} R)·µí·µñ where
  isTerminalTensorUnit := terminalOpOfInitial isInitialSelf
  fst := _
  snd := _
  tensorProductIsBinaryProduct S T :=
    BinaryCofan.IsColimit.op <| binaryCofanIsColimit (unop S) (unop T)
  fst_def S T := by ext x; show x ‚äó‚Çú 1 = x ‚äó‚Çú algebraMap R (unop T:) 1; simp
  snd_def S T := by ext x; show 1 ‚äó‚Çú x = algebraMap R (unop S:) 1 ‚äó‚Çú x; simp

instance : BraidedCategory (CommAlgCat.{u} R)·µí·µñ where
  braiding S T := .op <| isoMk (Algebra.TensorProduct.comm R _ _)
  braiding_naturality_right := by intros; ext <;> rfl
  braiding_naturality_left := by intros; ext <;> rfl
  hexagon_forward S T U := by ext <;> rfl
  hexagon_reverse S T U := by ext <;> rfl

open MonoidalCategory

variable {A B C D : (CommAlgCat.{u} R)·µí·µñ}

@[simp] lemma coe_tensorObj_unop : (A ‚äó B).unop = A.unop ‚äó[R] B.unop := rfl

@[simp] lemma coe_tensorUnit_unop : (ùüô_ (CommAlgCat.{u} R)·µí·µñ).unop = R := rfl

@[simp] lemma rightWhisker_hom (f : A ‚ü∂ B) :
    (f ‚ñ∑ C).unop.hom = Algebra.TensorProduct.map f.unop.hom (.id _ _) := rfl

@[simp] lemma leftWhisker_hom (f : A ‚ü∂ B) :
    (C ‚óÅ f).unop.hom = Algebra.TensorProduct.map (.id _ _) f.unop.hom := rfl

@[simp] lemma associator_hom_unop_hom :
    (Œ±_ A B C).hom.unop.hom =
      (Algebra.TensorProduct.assoc R R A.unop B.unop C.unop).symm.toAlgHom := rfl

@[simp] lemma associator_inv_unop_hom :
    (Œ±_ A B C).inv.unop.hom = (Algebra.TensorProduct.assoc R R A.unop B.unop C.unop).toAlgHom := rfl

@[simp] lemma braiding_hom_unop_hom :
    (Œ≤_ A B).hom.unop.hom = (Algebra.TensorProduct.comm R B.unop A.unop).toAlgHom := rfl

@[simp] lemma braiding_inv_unop_hom :
    (Œ≤_ A B).inv.unop.hom = (Algebra.TensorProduct.comm R A.unop B.unop).toAlgHom := rfl

@[simp] lemma tensorHom_unop_hom (f : A ‚ü∂ C) (g : B ‚ü∂ D) :
    (f ‚äó g).unop.hom = Algebra.TensorProduct.map f.unop.hom g.unop.hom := rfl

@[simp] lemma toUnit_unop_hom (A : (CommAlgCat R)·µí·µñ) :
    (toUnit A).unop.hom = Algebra.ofId R A.unop := rfl

@[simp] lemma fst_unop_hom (A B : (CommAlgCat R)·µí·µñ) :
    (fst A B).unop.hom = Algebra.TensorProduct.includeLeft := rfl

@[simp] lemma snd_unop_hom (A B : (CommAlgCat R)·µí·µñ) :
    (snd A B).unop.hom = Algebra.TensorProduct.includeRight := rfl

@[simp] lemma lift_unop_hom (f : C ‚ü∂ A) (g : C ‚ü∂ B) :
    (lift f g).unop.hom = Algebra.TensorProduct.lift f.unop.hom g.unop.hom (fun _ _ ‚Ü¶ .all _ _) :=
  rfl

end CommAlgCat
